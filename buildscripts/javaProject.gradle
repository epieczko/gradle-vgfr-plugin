apply plugin: 'java'
apply plugin: 'net.ltgt.apt'
apply from: rootProject.file('buildscripts/cloverProject.gradle')

sourceCompatibility = targetCompatibility = 1.8
configurations {
    compileOnly.extendsFrom annotationProcessor
    testCompileOnly.extendsFrom annotationProcessor
}
dependencies {
    annotationProcessor "org.projectlombok:lombok:1.16.20"
    compile 'com.google.code.findbugs:jsr305:3.0.2'
}

task testJar(type: Jar) {
    classifier = 'tests'
    from sourceSets.test.output
}

configurations {
    testArtifacts
}

artifacts {
    testArtifacts testJar
}

test {
    doFirst {
        sourceSets.each { sourceSet ->
            sourceSet.output.classesDirs.each { it.mkdirs() }
        }
    }
    testLogging {
        events "failed", "standardOut", "standardError"
        exceptionFormat "full"
        showCauses = true
        showStackTraces = true
    }
}