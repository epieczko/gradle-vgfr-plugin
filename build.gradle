group = 'tane.mahuta.build'

buildscript {

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }

    dependencies {
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.15"
        classpath 'com.bmuschko:gradle-clover-plugin:2.2.5'
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.7'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
}

allprojects {
    apply plugin: 'maven'
}

apply from: file('buildscripts/cloverProject.gradle')

subprojects {

    buildscript.repositories.addAll(rootProject.repositories)
    repositories.addAll(buildscript.repositories)

    group = rootProject.group
    version = rootProject.version

    subprojects {
        apply plugin: 'com.jfrog.bintray'

        bintray {
            user = System.getenv("BINTRAY_USER")
            key = System.getenv("BINTRAY_KEY")
            configurations = ['archives']
            pkg {
                repo = 'gradle-plugins'
                name = 'gradle-vgfr-plugin'
                userOrg = System.getenv("BINTRAY_USER")
                licenses = ['Apache-2.0']
                githubRepo = 'Tanemahuta/gradle-vgfr-plugin/'
                //githubReleaseNotesFile = 'README.md'
                vcsUrl = 'https://github.com/Tanemahuta/gradle-vgfr-plugin.git'
                version {
                    name = project.version as String
                    released = new Date()
                    if (!(project.version as String).endsWith("-SNAPSHOT")) {
                        vcsTag = "${project.version}"
                    }
                }
            }
            publish = true
        }
        tasks.bintrayUpload.onlyIf { !(project.version as String).endsWith("-SNAPSHOT") }
    }

}
